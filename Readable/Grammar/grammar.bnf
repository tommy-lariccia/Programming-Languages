program ::= statementList?

statementList ::= statement+

statement ::= progStatement comment? lineEndChar
            | comment lineEndChar
            | commentBlock lineEndChar

progStatement ::= functionDefinition
                | functionCall
                | variableInitializationAssignment
                | conditionalStatement
                | loopStatement

lineEndChar ::= "\n" | "\r"


// COMMENTS
comment ::= "//" (!"\n")*  // anything but newline character

commentBlock ::= "/*" (!"*\\")* "*\\"


// TYPES
booleanOpts ::= "true" | "false"

typing ::= typingKeywords | typingArr

typingArr ::= typingKeywords"[]"

typingKeywords ::= "int" | "str" | "bool" | "float"


// VARIABLES
variableInitializationAssignment ::= assignment | arrSlotAssign

assignment ::= assignmentPrefix? IDENTIFIER "=" expression

assignmentPrefix ::= "local" | typing  // mutually exclusive, local only for dynamic variables

arrSlotAssign ::= typingArr? IDENTIFIER"["NUMBER"]" "=" expression

arr ::= "[" exprList? "]"

arrAccess ::= IDENTIFIER"["NUMBER"]"

exprList ::= expression
            | expression "," exprList


// EXPRESSIONS
expression ::= binaryExpression

binaryExpression ::= unaryExpression binaryOp binaryExpression
                   | unaryExpression

binaryOp ::= '*' | '/' | '+' | '-' | "and" | "or" | "==" | ">" | "<" | ">=" | "<="

unaryExpression ::= frontUnaryExpression | backUnaryExpression | terminalExpression  // no use for combining unaries

frontUnaryExpression ::= frontUnaryOperator terminalExpression

backUnaryExpression ::= terminalExpression backUnaryOperator

frontUnaryOperator ::= "-" | "!"

backUnaryOperator ::= "++" | "--"

parenExpression ::= "(" expression ")"

terminalExpression ::= functionCall | NUMBER | IDENTIFIER | STRING | parenExpression | booleanOpts | arr | arrAccess


// BLOCKS
block ::= QUADSPACE statement
        | QUADSPACE statement lineEndChar block


// FUNCTIONS
functionCall ::= IDENTIFIER "(" argList? ")"

argList ::= expression
          | expression "," argList

paramList ::= parameter
            | parameter "," paramList

parameter ::= typing? IDENTIFIER

returnStatement ::= "return" expression

functionDefinition ::= "func" typing? IDENTIFIER "(" paramList? ")" ":" lineEndChar block QUADSPACE returnStatement

lambdaInitialization ::= IDENTIFIER "=" "(" paramList? ")" "=>" expression


// LOOPS
loopStatement ::= whileLoop | forEachLoop

whileLoop ::= "while" expression ":" lineEndChar block

forEachLoop ::= "foreach" IDENTIFIER "in" iterable ":" lineEndChar block

iterable ::= arr | NUMBER | range

range ::= NUMBER".."NUMBER


// CONDITIONALS
conditionalStatement ::= ifBlock elifBlock* elseBlock?

ifBlock ::= "if" expression ":" lineEndChar block

elifBlock ::= lineEndChar "else" "if" expression ":" lineEndChar block

elseBlock ::= lineEndChar "else" expression ":" lineEndChar block