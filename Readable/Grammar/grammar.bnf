// GRAMMAR - Tommy Lariccia
// https://github.com/tommy-lariccia/Programming-Languages/tree/main/Readable


program ::= statement*

statement ::= progStatement comment?
            | comment
            | commentBlock

progStatement ::= functionDefinition lineEndChar
                | functionCall lineEndChar
                | variableInitializationAssignment lineEndChar
                | conditionalStatement
                | loopStatement lineEndChar
                | lambdaInitialization lineEndChar
                | returnStatement lineEndChar

blockStatement ::= blockProgStatement comment? lineEndChar
                 | comment lineEndChar
                 | commentBlock lineEndChar

blockProgStatement ::= functionCall lineEndChar
                     | variableInitializationAssignment lineEndChar
                     | conditionalStatement
                     | loopStatement lineEndChar
                     | lambdaInitialization lineEndChar
                     | returnStatement lineEndChar

lineEndChar ::= "\n" | "\r" | EOF


// COMMENTS
comment ::= "//" (!"\n")*  // anything but newline character

commentBlock ::= "/*" (!"*\\")* "*\\"

// VARIABLES
variableInitializationAssignment ::= assignment | arrSlotAssign

assignment ::= IDENTIFIER "=" expression

arrSlotAssign ::= arrAccess "=" expression

arr ::= "[" exprList? "]"

arrAccess ::= IDENTIFIER "[" expression "]"

exprList ::= argList


// EXPRESSIONS
expression ::= binaryExpression

binaryExpression ::= unaryExpression binaryOp binaryExpression
                   | unaryExpression

binaryOp ::= '*' | '/' | '+' | '-' | "and" | "or" | "==" | ">" | "<" | ">=" | "<="

unaryExpression ::= frontUnaryExpression | terminalExpression  // no use for combining unaries

frontUnaryExpression ::= frontUnaryOperator terminalExpression

frontUnaryOperator ::= "-" | "!"

parenExpression ::= "(" expression ")"

terminalExpression ::= functionCall | INT_LIT | FLOAT_LIT | IDENTIFIER | STRING_LIT | parenExpression | booleanOpts |
                       arr | arrAccess | "null"


// BLOCKS
block ::= blockStart blockSpace

blockStart ::= ":" lineEndChar

blockSpace ::= QUADSPACE blockStatement
        | QUADSPACE blockStatement lineEndChar blockSpace

// FUNCTIONS
functionCall ::= IDENTIFIER "(" argList? ")"
               | functionCall "(" argList? ")"  // left-recursion, but surprisingly easy to handle in this case

argList ::= argument
          | argument "," argList

paramList ::= parameter
            | parameter "," paramList

parameter ::= ('*')? IDENTIFIER

argument ::= ('*')? expression

returnStatement ::= "return" expression

functionDefinition ::= "func" IDENTIFIER "(" paramList? ")" block

lambdaInitialization ::= "lambda" IDENTIFIER "=" "(" paramList? ")" "=>" expression


// LOOPS
loopStatement ::= whileLoop | forEachLoop

whileLoop ::= "while" expression block

forEachLoop ::= "foreach" IDENTIFIER "in" iterable block

iterable ::= arr | INT_LIT | range | STRING_LIT

range ::= expression".."expression


// CONDITIONALS
conditionalStatement ::= ifBlock elifBlock* elseBlock?

ifBlock ::= "if" expression block

elifBlock ::= "else" "if" expression block

elseBlock ::= "else" expression block